DROP TABLE IF EXISTS u_data;
DROP TABLE IF EXISTS u_data_new;

-- Create Source Table
CREATE TABLE u_data (userid INT, movieid INT, rating INT, unixtime STRING)
ROW FORMAT DELIMITED
FIELDS TERMINATED BY '\t'
STORED AS TEXTFILE;

LOAD DATA LOCAL INPATH '[[DATA_DIR]]/u.data' OVERWRITE INTO TABLE u_data;

-- Create Target Table
CREATE TABLE u_data_new (userid INT, movieid INT, rating INT, weekday INT)
ROW FORMAT DELIMITED
FIELDS TERMINATED BY '\t';

-- Query out data
SELECT COUNT(*), COUNT(distinct userid) FROM u_data;

-- Add MapReduce Python MR Job
ADD FILE [[DATA_DIR]]/weekday_mapper.py;

-- Transform and Insert Data from Source to Target
INSERT OVERWRITE TABLE u_data_new
SELECT TRANSFORM (userid, movieid, rating, unixtime)
  USING 'python weekday_mapper.py' AS (userid, movieid, rating, weekday)
FROM u_data;

-- Query Data out of Target
SELECT weekday, COUNT(*) FROM u_data_new GROUP BY weekday;

-- Query Data out of Source
SELECT userid, count(*) FROM u_data 
WHERE userid > 100 and userid < 150 
GROUP BY userid 
ORDER BY userid;

SELECT * FROM u_data 
ORDER BY userid, movieid, rating 
LIMIT 20;

SELECT userid, movieid, rating FROM u_data 
ORDER BY userid, movieid, rating 
LIMIT 20;
